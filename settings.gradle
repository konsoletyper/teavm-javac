/*
 *  Copyright 2023 Alexey Andreev.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

pluginManagement {
     repositories {
         maven { url = uri("https://teavm.org/maven/repository") }
         mavenCentral()
         mavenLocal()
         gradlePluginPortal()
     }
}

dependencyResolutionManagement {
    repositories {
        maven { url = uri("https://teavm.org/maven/repository") }
        mavenCentral()
        mavenLocal()
    }
}


gradle.allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }
}

gradle.afterProject {
    def java = extensions.findByType(JavaPluginExtension)
    if (java != null) {
        apply(plugin: CheckstylePlugin)
        extensions.configure(CheckstyleExtension) {
            toolVersion = extensions.getByType(VersionCatalogsExtension).named("libs")
                    .findVersion("checkstyle").get().requiredVersion
        }
        java.sourceCompatibility = JavaVersion.VERSION_21
        java.targetCompatibility = JavaVersion.VERSION_21
    }
}

include "javac"
include "compiler"
include "protocol"
include "ui"